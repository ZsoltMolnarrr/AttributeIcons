plugins {
    id 'fabric-loom' version '1.11-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

def platform = "fabric"

base {
    archivesName = project.archives_base_name
}


fabricApi {
    configureDataGeneration {
        client = true
        modId = "attributeicons"
    }
}

repositories {
    maven { url 'https://jitpack.io' }
    maven { url 'https://maven.kosmx.dev/' }
    maven { url 'https://maven.shedaniel.me/' }
    maven { url 'https://maven.azuredoom.com/mods' }
    maven { url 'https://maven.terraformersmc.com/' }
    maven {
        name = 'Modrinth'
        url = 'https://api.modrinth.com/maven'
        content {
            includeGroup 'maven.modrinth'
        }
    }
    maven {
        name = "Ladysnake Libs"
        url = 'https://maven.ladysnake.org/releases'
    }
    // CurseForge Maven
    maven { url "https://cursemaven.com" }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    include(implementation("com.github.ZsoltMolnarrr:TinyConfig:${project.tiny_config_version}"))

    modImplementation("maven.modrinth:better-combat:${project.better_combat_version}")
    modImplementation("maven.modrinth:combat-roll:${project.combat_roll_version}")

    modImplementation("maven.modrinth:ranged-weapon-api:${project.ranged_weapon_api_version}-${platform}")
    modImplementation("maven.modrinth:bundle-api:${project.bundle_api_version}-${platform}")
    modImplementation("maven.modrinth:spell-engine:${project.spell_engine_version}-${platform}")
    modImplementation("maven.modrinth:spell-power:${project.spell_power_version}-${platform}")
    modImplementation("dev.kosmx.player-anim:player-animation-lib-${platform}:${project.player_anim_version}")
    modImplementation("me.shedaniel.cloth:cloth-config-${platform}:${project.cloth_config_version}")
    modImplementation("dev.emi:trinkets:${project.trinkets_version}")

    modImplementation("curse.maven:critical-strike-1379562:${project.critical_strike_version}")

    modImplementation("maven.modrinth:more-rpg-library:${project.more_rpg_library_version}-${platform}")
    modImplementation("maven.modrinth:witcher-rpg-class:${project.witcher_version}-${platform}")
    modImplementation("mod.azure.azurelibarmor:azurelibarmor-${platform}-${project.azure_mc_version}:${project.azure_lib_version}")
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version
    }
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}

// Post-process generated JSON files to fix unicode escaping
tasks.register('postProcessDatagen') {
    group = 'fabric'
    description = 'Post-processes generated JSON files to replace double backslashes with single backslashes'

    doLast {
        def generatedDir = file('src/main/generated')
        if (!generatedDir.exists()) {
            println "Generated directory does not exist, skipping post-processing"
            return
        }

        def processedCount = 0
        generatedDir.eachFileRecurse { file ->
            if (file.name.endsWith('.json')) {
                def content = file.text
                def modified = content.replace('\\\\u', '\\u')
                if (content != modified) {
                    file.text = modified
                    processedCount++
                    println "Post-processed: ${file.path}"
                }
            }
        }
        println "Post-processing complete: ${processedCount} file(s) modified"
    }
}

// Automatically run post-processing after datagen
tasks.named('runDatagen') {
    finalizedBy 'postProcessDatagen'
}
